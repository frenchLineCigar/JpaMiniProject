plugins {
	id 'org.springframework.boot' version '2.3.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
}

//ext["hibernate.version"] = "5.4.20.Final" //하이버네이트 버전 지정

group = 'jpabook'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-devtools'

	implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.5.6' //쿼리 파라미터를 로그로 남기는 외부 라이브러리 p6spy

	implementation 'com.querydsl:querydsl-jpa'
	implementation 'com.querydsl:querydsl-apt'

	annotationProcessor("com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa") // Using QueryDSL JPAAnnotationProcessor
	annotationProcessor("javax.persistence:javax.persistence-api") // Java 9 or later : java.lang.NoClassDefFoundError (javax.annotation.Entity)
	annotationProcessor("javax.annotation:javax.annotation-api") // Java 9 or later : java.lang.NoClassDefFoundError (javax.annotation.Generated)

	runtimeOnly 'com.h2database:h2'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		//exclude group: 'org.junit.vintage', module: 'junit-vintage-engine' //부트 상위버전에서 JUnit4 사용하려면 junit-vintage-engine 제외되지 않도록 주석처리
	}
}

test {
	useJUnitPlatform()
}

/* QueryDSL 설정 추가 */
def generationPath = file("src/main/generated/")

// 어노테이션 프로세서에 의해 자동 생성된 소스코드가 저장될 경로 지정
compileJava {
	options.annotationProcessorGeneratedSourcesDirectory = generationPath
}

// clean Task 실행시에 자동 생성된 소스코드를 모두 삭제한다
clean.doLast {
	generationPath.deleteDir()
}

// 자동 생성된 소스코드의 경로도 소스셋에 포함한다
sourceSets {
	main {
		java {
			srcDirs += generationPath
		}
	}
}
/* QueryDSL 설정 끝 */